AC_INIT(src/test.cpp)
AM_INIT_AUTOMAKE(Otawa, 0.1)
AM_CONFIG_HEADER(config.h)


AC_PROG_CXX
AC_PROG_RANLIB
AC_PROG_YACC
AM_PROG_LEX
AC_STDC_HEADERS
AC_HAVE_HEADERS(string.h)


AC_ARG_ENABLE(doc,
	[  --enable-doc		  Enable documentation generation [default=yes]],
	,
	enable_doc=yes)
if test $enable_doc = yes; then
	AC_CHECK_PROG(USE_DOX,doxygen,yes)
fi


AC_ARG_WITH(elm, [  --with-elm[=PATH]	Set the path to the Elm library (default $PWD/elm).],,with_elm=yes)
if test $with_elm = 'no'; then
	AC_MSG_ERROR([Elm library is mandatory.])
elif test $with_elm = 'yes'; then
	ELM_PATH=$PWD/elm
	AC_MSG_RESULT([Assuming elm at $ELM_PATH.])
else
	ELM_PATH=$with_elm
fi
AC_SUBST(ELM_PATH)


AC_ARG_ENABLE(heptane, [  --enable-heptane [default=yes]],,enable_heptane=yes)
if test $enable_heptane = 'yes'; then
	HEPTANE_LIB=heptane
	HEPTANE_CFLAGS=-DHEPTANE_ENABLED
	HEPTANE_LDADD="-L$PWD/src/heptane -lothept"
fi
AC_SUBST(HEPTANE_LIB)
AC_SUBST(HEPTANE_CFLAGS)
AC_SUBST(HEPTANE_LDADD)


AC_ARG_WITH(glissppc, [  --with-glissppc[=PATH]  Add support for Gliss PPC (default yes)],,with_glissppc=yes)
if test $with_glissppc = no; then
	if test $enable_heptane = yes; then
		AC_MSG_WARN([glissppc is mandatory when using heptane.])
		with_glissppc=yes
	fi
fi
if test $with_glissppc != no; then
	if test $with_glissppc = yes; then
		GLISSPPC_PATH=$PWD/gliss-ppc
		AC_MSG_RESULT([Assuming Gliss PPC at $GLISSPPC_PATH.])
	else
		GLISSPPC_PATH=$with_glissppc
	fi
	GLISSPPC_LIB=gliss
	GLISSPPC_LDADD="-L$PWD/src/gliss -lgliss -L$GLISSPPC_PATH/lib -lemul"
	GLISSPPC_CFLAGS="-DGLISSPPC_ENABLED -I$GLISSPPC_PATH/include -I$GLISSPPC_PATH/extern"
fi
AC_SUBST(GLISSPPC_LIB)
AC_SUBST(GLISSPPC_LDADD)
AC_SUBST(GLISSPPC_CFLAGS)


AC_OUTPUT([
	Makefile
	src/Makefile
	src/prog/Makefile
	src/gliss/Makefile
	src/oshell/Makefile
	src/heptane/Makefile
	include/Makefile
	include/otawa/Makefile
	include/otawa/ast/Makefile
	include/otawa/cfg/Makefile
	include/otawa/gliss/Makefile
	include/otawa/prog/Makefile
	include/otawa/prop/Makefile
	examples/Makefile
	examples/dumpcfg/Makefile
], [])
